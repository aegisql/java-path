options
{
  JDK_VERSION = "1.8";
  IGNORE_CASE=false;
  VISITOR=true;
  MULTI=true;
//  DEBUG_PARSER=true;
//  DEBUG_LOOKAHEAD=true ;
//  FORCE_LA_CHECK=true;
//  DEBUG_TOKEN_MANAGER=true;
  UNICODE_INPUT=true;
}

PARSER_BEGIN(CCJavaPathParser)
package com.aegisql.java_path.parser;

import com.aegisql.java_path.TypedValue;
import java.util.LinkedList;
import java.util.stream.Collectors;

public class CCJavaPathParser {}

PARSER_END(CCJavaPathParser)

TOKEN :
{
  <K_LBRACE: "{">
| <K_RBRACE: "}">
| <K_LPARENTHESIS : "(">
| <K_RPARENTHESIS : ")">
| <K_DOT: ".">
| <K_COMMA: ",">
| <K_SPACE: ([" ","\t","\n","\r"])+>
| < #LETTER: ["_","a"-"z","A"-"Z"] >
| < #DIGIT: ["0"-"9"] >
| < #SPECIAL: ["@","$","#"] >
| < K_PATH_ELEMENT: ( <LETTER> | <DIGIT> | <SPECIAL> )+ >
| <K_QUOTED_STRING: "\"" ( (~["\"","\\"]) | ("\\" ( ["n","t","b","r","f","\\","\""] ) ) )* "\"">
| <K_SINGLE_QUOTED_STRING: "\'" ( (~["\'","\\"]) | ("\\" ( ["n","t","b","r","f","\\","\'"] ) ) )* "\'">
}

/** Root production. */
SimpleNode fullPath() :
{
    String type = null;
}
{
    (
        <K_LPARENTHESIS> [type = fullType()] typedPathElement() <K_RPARENTHESIS>
        |
        typedPathElement()
    ) [<K_DOT> fullPath() ]
    <EOF>
    {
        jjtThis.jjtSetValue(type);
        return jjtThis;
    }

}

void typedPathElement() #void:
{}
{
    pathElement() <K_LBRACE> parameters() <K_RBRACE>
    |
    pathElement()
}

void pathElement() :
{
    Token t = null;
}
{
    t = <K_PATH_ELEMENT>
    {
        jjtThis.jjtSetValue(t.image);
    }
}

String fullType() #void:
{
    LinkedList<String> list = null;
}
{
    list = type() <K_SPACE>
    {
        return list.stream().collect(Collectors.joining("."));
    }
}

LinkedList type() #void:
{
    Token t = null;
    LinkedList<String> typeElements = new LinkedList<String>();
    LinkedList<String> subElements = null;
}
{
    t = <K_PATH_ELEMENT> [<K_DOT> subElements = type() {
        typeElements.addAll(subElements);
    } ]
    {
        typeElements.addFirst(t.image);
        return typeElements;
    }
}

void parameters() :
{
    String type = null;
    String value = null;
}
{
    (
        type = fullType() value = parameter()
        |
        value = parameter()
    ) [<K_COMMA> parameters() ]
    {
        TypedValue tv = new TypedValue();
        tv.setType(type);
        tv.setValue(value);
        jjtThis.jjtSetValue(tv);
    }
}

String parameter() #void:
{
    Token t = null;
}
{
    (t = <K_PATH_ELEMENT> | t = <K_QUOTED_STRING> | t = <K_SINGLE_QUOTED_STRING>)
    {
        return t.image;
    }
}
