options
{
  JDK_VERSION = "1.8";
  IGNORE_CASE=false;
  STATIC=false;
  VISITOR=true;
  MULTI=true;
//  DEBUG_PARSER=true;
//  DEBUG_LOOKAHEAD=true ;
//  FORCE_LA_CHECK=true;
//  DEBUG_TOKEN_MANAGER=true;
  UNICODE_INPUT=true;
}

PARSER_BEGIN(CCJavaPathParser)
package com.aegisql.java_path.parser;

import com.aegisql.java_path.TypedValue;
import java.util.LinkedList;
import java.util.stream.Collectors;

public class CCJavaPathParser
{
}

PARSER_END(CCJavaPathParser)

TOKEN :
{
  <K_LBRACE: "{">
| <K_RBRACE: "}">
| <K_LPARENTHESIS : "(">
| <K_RPARENTHESIS : ")">
| <K_DOT: ".">
| <K_COMMA: ",">
| <K_ESC: "\\">
| <K_SPACE: ([" ","\t"])+>
| < K_PATH_ELEMENT: ( ["a"-"z","A"-"Z","_","0"-"9","@","$","#"] )+ >
| < K_IDENTIFIER: <LETTER> (<LETTER>|<DIGIT>)* >
| < #LETTER: ["_","a"-"z","A"-"Z"] >
| < #DIGIT: ["0"-"9"] >
| <K_QUOTED_STRING: "\"" ( (~["\"","\\"]) | ("\\" ( ["n","t","b","r","f","\\","\""] ) ) )* "\"">
| <K_SINGLE_QUOTED_STRING: "\'" ( (~["\'","\\"]) | ("\\" ( ["n","t","b","r","f","\\","\'"] ) ) )* "\'">
}

/** Root production. */
SimpleNode fullPath() :
{
    String type = null;
}
{
    (
        LOOKAHEAD(2) <K_LPARENTHESIS> [type = fullType()] typedPathElement() <K_RPARENTHESIS> [<K_DOT> fullPath() ]
        |
        typedPathElement() [<K_DOT> fullPath() ]
    )
    <EOF>
    {
        jjtThis.jjtSetValue(type);
        return jjtThis;
    }

}

void typedPathElement() :
{}
{
    (
        pathElement() <K_LBRACE> parameters() <K_RBRACE>
        |
        pathElement()
    )
}

void pathElement() :
{
    Token t = null;
}
{
    t = <K_PATH_ELEMENT>
    {
        jjtThis.jjtSetValue(t.image);
    }
}

String fullType() :
{
    LinkedList<String> t = null;
}
{
    t = type() <K_SPACE>
    {
        return t.stream().collect(Collectors.joining("."));
    }
}

LinkedList type() :
{
    Token t = null;
    LinkedList<String> typeElements = new LinkedList<String>();
    LinkedList<String> subElements = null;
}
{
    t = <K_PATH_ELEMENT>[<K_DOT> subElements = type() {
        typeElements.addAll(subElements);
    } ]
    {
        typeElements.addFirst(t.image);
    }
    {
        return typeElements;
    }
}

void parameters() :
{
    String type = null;
    String value = null;
}
{
    (
        type = fullType() value = parameter()[<K_COMMA> parameters() ]
        |
        value = parameter()[<K_COMMA> parameters() ]
    ){
        TypedValue tv = new TypedValue();
        tv.setType(type);
        tv.setValue(value);
        jjtThis.jjtSetValue(tv);
    }
}

String parameter() :
{
    Token t = null;
 }
{
    (t = <K_PATH_ELEMENT> | t = <K_QUOTED_STRING> | t = <K_SINGLE_QUOTED_STRING>)
    {
        return t.image;
    }
}
